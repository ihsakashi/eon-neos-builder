ANDROID_BUILD_TOP=$$(ANDROID_BUILD_TOP)
isEmpty(ANDROID_BUILD_TOP) : error("$ANDROID_BUILD_TOP is empty. Forgot to run 'lunch'?")

ANDROID_PRODUCT_OUT=$$(ANDROID_PRODUCT_OUT)
isEmpty(ANDROID_PRODUCT_OUT) : error("$ANDROID_PRODUCT_OUT is empty. forgot to run 'lunch'?")

QT_QPA_DEFAULT_PLATFORM = eglfs
EGLFS_DEVICE_INTEGRATION    = eglfs_surfaceflinger

MAKEFILE_GENERATOR      = UNIX
CONFIG                 += incremental
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/clang.conf)

load(device_config)

ANDROID_PLATFORM        = android-23
CONFIG                 += $$ANDROID_PLATFORM
#QMAKE_CFLAGS            = -D__ANDROID_API__=$$replace(ANDROID_PLATFORM, "android-", "")

DEFINES                += Q_OS_ANDROID_EMBEDDED
CONFIG                 += android-embedded

NDK_HOST                = linux-x86_64
NDK_ROOT                = @NDK_ROOT@
NDK_LLVM_PATH           = $$NDK_ROOT/toolchains/llvm/prebuilt/$$NDK_HOST

QMAKE_CC                = $${NDK_LLVM_PATH}/bin/aarch64-linux-android$$replace(ANDROID_PLATFORM, "android-", "")-clang
QMAKE_CXX               = $${NDK_LLVM_PATH}/bin/aarch64-linux-android$$replace(ANDROID_PLATFORM, "android-", "")-clang++
QMAKE_LINK              = $$QMAKE_CXX
QMAKE_LINK_SHLIB        = $$QMAKE_CXX
QMAKE_AR                = $${NDK_LLVM_PATH}/bin/aarch64-linux-android-ar cqs
QMAKE_OBJCOPY           = $${NDK_LLVM_PATH}/bin/aarch64-linux-android-objcopy
QMAKE_NM                = $${NDK_LLVM_PATH}/bin/aarch64-linux-android-nm -P
QMAKE_PKG_CONFIG        = $${NDK_LLVM_PATH}/bin/aarch64-linux-android-pkg-config
QMAKE_STRIP             = $${NDK_LLVM_PATH}/bin/aarch64-linux-android-strip
QMAKE_RANLIB            = $${NDK_LLVM_PATH}/bin/aarch64-linux-android-ranlib

QMAKE_CFLAGS           += --sysroot=$$[QT_SYSROOT] -fstack-protector-strong -Oz
QMAKE_CFLAGS           += -isystem $$[QT_SYSROOT]/usr/include/aarch64-linux-android \
                          -isystem $$[QT_SYSROOT]/usr/include/c++/v1
QMAKE_CXXFLAGS         += $$QMAKE_CFLAGS
QMAKE_LFLAGS           += -L$${ANDROID_PRODUCT_OUT}/obj/lib -Wl,-rpath=/data/data/com.termux/files/usr/lib -Wl,-rpath=/system/lib64 -Wl,--enable-new-dtags -Wl,--as-needed -Wl,-z,relro,-z,now

QMAKE_LFLAGS_SHLIB     += -shared
QMAKE_LFLAGS_PLUGIN    += -shared

QMAKE_LIBS_THREAD       =

QMAKE_LIBS_EGL          = -lEGL
QMAKE_LIBS_OPENGL_ES2   = -lGLESv2

load(qt_config)
